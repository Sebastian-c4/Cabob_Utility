
<<<<<<<< .../c4EEinlined/c4!spcre.baf
>>>>>>>>


PRINT ~********** 移除旧设置…… **********~
ACTION_IF FILE_EXISTS_IN_GAME ~c#crefig.bcs~ BEGIN
	INCLUDE ~%EES%/Remove_V1.tph~
END

/* **************************************** */
/* V2 */
/* **************************************** */

/* 防止与英雄/威力药水叠加造成的无敌状态, 以及未知原因的锁血 */
ACTION_FOR_EACH suffix IN 09 BEGIN
	COPY_EXISTING ~potn%suffix%.itm~ override
		GET_OFFSET_ARRAY header ITM_V10_HEADERS
		PHP_EACH header AS int => index BEGIN
			GET_OFFSET_ARRAY2 effect index ITM_V10_HEAD_EFFECTS
			PHP_EACH effect AS int => offset BEGIN
				READ_SHORT offset opcode
				READ_LONG offset+8 parameter2
				PATCH_IF opcode=18 && parameter2=2 BEGIN
					READ_BYTE offset+2 target
					READ_BYTE offset+3 power
					READ_LONG offset+4 parameter1
					READ_BYTE offset+0xc timing
					READ_BYTE offset+0xd resist_dispel
					READ_LONG offset+0xe duration
					READ_BYTE offset+0x12 probability1
					READ_BYTE offset+0x13 probability2
					READ_ASCII offset+0x14 resource
					READ_LONG offset+0x1c dicenumber
					READ_LONG offset+0x20 dicesize
					READ_LONG offset+0x24 savingthrow
					READ_LONG offset+0x28 savebonus
					INNER_ACTION BEGIN
						COPY_EXISTING spcl131.spl ~override/c4!ptn%suffix%.spl~
							LPF ALTER_SPELL_HEADER INT_VAR projectile=79 END
							LPF ALTER_EFFECT INT_VAR opcode target power parameter1 parameter2 timing duration resist_dispel probability1 probability2 dicenumber dicesize savingthrow savebonus STR_VAR resource END
					END
					LPF ALTER_EFFECT INT_VAR match_opcode=18 opcode=146 target=1 timing=1 parameter1=10 parameter2=1 STR_VAR resource=~c4!ptn%suffix%~ END
				END
			END
		END
		
	COPY_EXISTING ~potn%suffix%.itm~ ~override/dw#ptn%suffix%.itm~
END

PRINT ~********** 环境定义…… **********~
INCLUDE ~%EES%/Defination.tph~

PRINT ~********** 脚本创建…… **********~
INCLUDE ~%EES%/Scripts_Creation.tph~

PRINT ~********** 法术创建…… **********~
INCLUDE ~%EES%/Create_Spells.tph~

ACTION_IF boostresist=1 BEGIN
	INCLUDE ~%EES%/Resistance_Part.tph~
END

PRINT ~********** 按自定义参数赋值…… **********~
AT_NOW ~%EES%/Enhanced_Enemies.txt~
INCLUDE ~%EES%/Assign_Values.tph~
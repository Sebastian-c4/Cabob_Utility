
/* //////////////////////////////////// */
/* 生物相关 */
/* 掉落修改和修正 */
/* //////////////////////////////////// */
	
//夜袭小队经验降为1/5，盔甲/武器/卷轴不掉落，医疗药水掉落2瓶，法师杀手掉落3支破魔箭或1支爆炸箭
//没装上...先留着好了...
COPY_EXISTING RNDTREAS.2da override
	COUNT_2DA_ROWS 20 row
	INSERT_2DA_ROW row 20 ~GBENE_POTION POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2 POTN55*2~
	SET row+=1
	SPRINT potion ~RNDTRE%row%~
	INSERT_2DA_ROW row 20 ~GBENE_AROW07 AROW07*3 AROW07*3 AROW07*3 AROW07*3 AROW07*3 AROW07*3 AROW07*3 AROW07*3 AROW07*3 AROW06 AROW06 AROW06 AROW06 AROW06 AROW06 AROW06 AROW06 AROW06 AROW06~
	SET row+=1
	SPRINT arrow ~RNDTRE%row%~
COPY_EXISTING RNDTRE01.itm ~override/%potion%.itm~
COPY_EXISTING RNDTRE01.itm ~override/%arrow%.itm~	

COPY_EXISTING_REGEXP GLOB ~GBENE0[12][A-F].cre~ override
	WRITE_LONG 0x14 THIS/5
	PATCH_IF ~%SOURCE_RES%~ STRING_CONTAINS_REGEXP ~1~ BEGIN
		ADD_CRE_ITEM ~%potion%~ #0 #0 #0 NONE INV11
	END
	PATCH_IF ~%SOURCE_RES%~ STR_EQ ~GBENE02C~ BEGIN
		ADD_CRE_ITEM ~%arrow%~ #0 #0 #0 NONE INV12
	END
	GET_OFFSET_ARRAY itm CRE_V10_ITEMS
	PHP_EACH itm AS int => itm_off BEGIN
		READ_ASCII itm_off itemname
		SET nodrop=0
		INNER_ACTION BEGIN
			COPY_EXISTING - ~%itemname%.itm~ override
				READ_SHORT 0x1c type
				READ_BYTE 0x18 flag
				PATCH_IF
							(~%itemname%~ STRING_CONTAINS_REGEXP ~RND.+~ 
								AND ~%itemname%~ STR_EQ ~MISC07~ = 0)
					AND (type=0x2
								OR type=0xb
								OR type=0xc
								OR type>0xe && type<0x1f
								OR ~%itemname%~ STR_EQ ~AROW07~
								OR ~%itemname%~ STR_EQ ~POTN55~
								OR (flag BAND 0b01000000) = 0b00000000)
				BEGIN
					SET nodrop=1
				END
		END
		PATCH_IF nodrop = 1 BEGIN
			LPF ADD_CRE_ITEM_FLAGS STR_VAR item_to_change=~%itemname%~ flags=UNDROPPABLE END
		END
	END
IF_EXISTS BUT_ONLY
	
//删除珊蒂儿身上重复掉落的物品	
	COPY_EXISTING_REGEXP GLOB ~CENTEO.*.cre~ override
		PATCH_FOR_EACH item IN RING18 RING23 MISC90 SW2H06 BEGIN
			LPF DELETE_CRE_ITEM STR_VAR item_to_delete = ~%item%~ END
		END
	BUT_ONLY
		
COPY_EXISTING_REGEXP GLOB ~.*\.cre~ override
	//尽可能降低BG1怪物掉落高级卷轴的可能性
	READ_BYTE 0x272 race
	PATCH_IF race=0x6e OR race=0x71 BEGIN
		GET_OFFSET_ARRAY item CRE_V10_ITEMS
		PHP_EACH item AS int => offset BEGIN
			READ_ASCII offset name
			PATCH_IF ~%name%~ STR_EQ RNDTRE04 OR ~%name%~ STR_EQ RNDTRE05 BEGIN
				WRITE_ASCII offset RNDTRE09
			END
		END
	END
	
	//删除生物背包里的魔法塞子的钥匙，改由脚本添加
	LPF DELETE_CRE_ITEM STR_VAR item_to_delete=bgmisc83 END
	
	/* 黑暗精灵城内的敌人不再掉落黑暗精灵独有装备 */
	//检查, 记录和替换黑暗精灵装备
	GET_OFFSET_ARRAY items CRE_V10_ITEMS
	PHP_EACH items AS int => offset BEGIN
		READ_ASCII offset has_item
		PATCH_FOR_EACH dark_elf_equipment IN blun01 bolt01 bolt02 bolt03 chan01 chan02 clck01 halb01 plat01 shld01 sper01 sw1h01 sw1h02 xbow01 BEGIN
			PATCH_IF ~%has_item%~ STR_EQ ~dw%dark_elf_equipment%~ BEGIN
				WRITE_ASCIIE offset ~c!%dark_elf_equipment%~
				SET $dark_elfs(~%SOURCE_RES%~)=1
			END
		END
	END
BUT_ONLY

ACTION_PHP_EACH dark_elfs AS creature => int BEGIN
	COPY_EXISTING ~%creature%.cre~ override
		SET script_set=0
		PATCH_FOR_EACH offset IN 0x248 0x250 0x258 0x260 0x268 BEGIN
			READ_ASCII offset script
			PATCH_IF script_set=0
				AND	((~%script%~ STR_EQ ~NONE~)	OR (STRING_LENGTH ~%script%~ =0))
			BEGIN
				WRITE_ASCII offset ~c!deout~
				SET script_set=1
			END
		END
	BUT_ONLY
END

COPY_EXISTING blank.bcs ~override/c!deout.bcs~

ACTION_FOR_EACH dark_elf_equipment IN blun01 bolt01 bolt02 bolt03 chan01 chan02 clck01 halb01 plat01 shld01 sper01 sw1h01 sw1h02 xbow01 BEGIN
	COPY_EXISTING ~dw%dark_elf_equipment%.itm~ ~override/c!%dark_elf_equipment%.itm~
		WRITE_BYTE 0x18 (THIS BAND 0b11111011)
	EXTEND_BOTTOM ~c!deout.bcs~ ~%SCRIPTS%/give_back_dark_elf_equipment.baf~ EVAL
END
	
//BG1冰岛上的生物不掉落自由行动戒指
COPY_EXISTING - ARU009.are override ARU010.are override
	GET_OFFSET_ARRAY cre ARE_V10_ACTORS
	PHP_EACH cre AS int => offset BEGIN
		READ_ASCII offset+0x80 crename
		INNER_ACTION BEGIN
			COPY_EXISTING ~%crename%.cre~ override
				GET_OFFSET_ARRAY cres CRE_V10_ITEMS
				PHP_EACH cres AS int => item_off BEGIN
					READ_ASCII item_off item
					PATCH_IF ~%item%~ STR_EQ RING09 BEGIN
						LPF DELETE_CRE_ITEM STR_VAR item_to_delete = RING09 END
						ADD_CRE_ITEM RING09 #0 #0 #0 UNDROPPABLE RRING
					END
				END
			BUT_ONLY
		END
	END
	
//七谷之巢小队不再掉落：邪恶大法师袍、大法师杖、天圣怒火、防御戒指+2、防御斗篷+2、防御护腕+3、火牙匕首、割喉者+4、丘陵巨人腰带、希达林护身符、夜之礼赞+5
ACTION_FOR_EACH cre IN 1 2 3 4 5 BEGIN
	COPY_EXISTING ~SEVPAT0%cre%.cre~ override
		PATCH_FOR_EACH item IN CLCK17 BRAC15 STAF11 SW1H51 RING07 CLCK02 DAGG12 SW1H28 BELT06 AMUL27 LEAT14 BEGIN
			LPF ADD_CRE_ITEM_FLAGS STR_VAR item_to_change=~%item%~ flags=~unstealable&undroppable~ END
		END
	BUT_ONLY
END
		
//伊塞掉落添加吸血鬼剑以供合成和升级
COPY_EXISTING ILLASERA.cre override
	ADD_CRE_ITEM SW1H19 #0 #0 #0 NONE INV16
BUT_ONLY
		
//删除LostItem在BG1各生物身上添加的各种次元袋
COPY_EXISTING_REGEXP ~.*\.cre~ override
	LPF DELETE_CRE_ITEM STR_VAR item_to_delete=~SWBAG.*~ END
BUT_ONLY
	
//修复蕾茵脚本，手持会隐形的大法杖 test
COPY_EXISTING HLLAYEN.bcs override
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~EquipItem("staff11")~ ~EquipItem("S#STAF02")~
	END
BUT_ONLY

COPY_EXISTING HLLAYEN.cre override
	ADD_CRE_ITEM S#STAF02 #0 #5 #1 UNDROPPABLE WEAPON1 EQUIP TWOHANDED
					

/* //////////////////////////////////// */
/* 杂项设置 */
/* 区域设置 */
/* //////////////////////////////////// */

//修正商店物品数量错误
COPY_EXISTING_REGEXP GLOB ~.*\.sto~ override
	GET_OFFSET_ARRAY itm STO_V10_ITEMS_SOLD
	PHP_EACH itm AS int => itm_off BEGIN
		READ_SHORT 	itm_off+0xa		c1
		READ_SHORT 	itm_off+0xc		c2
		READ_SHORT 	itm_off+0xe		c3
		READ_LONG		itm_off+0x18	limit
		SET tofix=0
		PATCH_IF c1=0 AND c2=0 AND c3=0 AND limit = 0 BEGIN
			READ_ASCII itm_off itemname
			INNER_ACTION BEGIN
				COPY_EXISTING - ~%itemname%.itm~ override
					READ_SHORT 0x1c type
					PATCH_IF type=0x9 OR type=0xb BEGIN
						SET tofix=1
					END
			END
		END
		PATCH_IF tofix=1 BEGIN
			WRITE_SHORT itm_off+0xa 1
			WRITE_SHORT itm_off+0xc 1
			WRITE_SHORT itm_off+0xe 1
		END
	END
BUT_ONLY
	
//所有商店不可偷窃，收价统一物品价格的25%，但不会视库存数量降低
COPY_EXISTING_REGEXP GLOB ~.*\.sto~ override
	READ_LONG 0x8 type
	PATCH_FOR_EACH type0 IN 0 1 2 3 BEGIN
		PATCH_IF type=type0 BEGIN
			WRITE_BYTE 0x10 THIS BAND 0b11110111
			WRITE_LONG 0x18 25
			WRITE_LONG 0x1c 0
		END
	END
BUT_ONLY
		
//删除烛堡里出售的次元袋
COPY_EXISTING inn2616.sto override
	LPF DELETE_STORE_ITEM STR_VAR item_to_delete = D1BG1BAG END
BUT_ONLY
		
//洛斯不再出售增加属性的书
COPY_EXISTING S#ROLLES.sto override
	FOR (b=1;b<9;b+=1) BEGIN
		REMOVE_STORE_ITEM ~book0%b%~
		REMOVE_STORE_ITEM ~s#book0%b%~
	END
BUT_ONLY

//利伯德和TOB秘术阁出售蛛网杖，不再是墓园区地下的蜘蛛女王派娜的任务奖励
COPY_EXISTING RIBALD.sto override 25SPELL.sto override
	ADD_STORE_ITEM ~WAND14~ FIRST #20 #0 #0 ~IDENTIFIED~ #1

//行脚商人不出售杂物（方便回购），但售价比例提升为300%
COPY_EXISTING SHOP04.sto override SHOP07.sto override
	WRITE_LONG 0x14 300
	LPF DELETE_STORE_ITEM END
				
//提高矮人秘银商人的售价至200%，无限量出售+3秘银箭/矢
COPY_EXISTING DESTO#02.sto override
	WRITE_LONG 0x14 200
	ADD_STORE_ITEM ~ADAAROW~ LAST #40 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
	ADD_STORE_ITEM ~ADABOLT~ LAST #40 #0 #0 ~IDENTIFIED~ #1 ~UNLIMITED~
BUT_ONLY
			
COPY_EXISTING AROW15.ITM ~override/ADAAROW.itm~
	WRITE_LONG 0x34 30
	GET_OFFSET_ARRAY head ITM_V10_HEADERS
	PHP_EACH head AS int => header BEGIN
		WRITE_ASCII header+0x04 ~IAROW10~
	END
	SAY 0xc 	@40201
	SAY 0x54 	@40202
					
COPY_EXISTING BOLT09.ITM ~override/ADABOLT.itm~
	WRITE_LONG 0x34 30
	GET_OFFSET_ARRAY head ITM_V10_HEADERS
	PHP_EACH head AS int => header BEGIN
		WRITE_ASCII header+0x04 ~IBOLT05~
	END
	SAY 0xc 	@40203
	SAY 0x54 	@40204

	